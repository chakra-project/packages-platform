#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=nvidia
pkgver=256.35
pkgrel=1
pkgdesc="NVIDIA beta drivers for kernel26." 
arch=('i686' 'x86_64') 

# Find the kernel name inside the chroot
_kernver=`pacman -Qf kernel26 | cut -c10-15 | sed 's/kernel26 //g'`-CHAKRA

[ "$CARCH" = "i686" ] && ARCH=x86 && _srcname=NVIDIA-Linux-x86-${pkgver}
[ "$CARCH" = "x86_64" ] && ARCH=x86_64 && _srcname=NVIDIA-Linux-x86_64-${pkgver}-no-compat32 
provides=("nvidia=${pkgver}")
replaces=('nvidia')
url="http://www.nvidia.com/" 
depends=('kernel26' "nvidia-utils=${pkgver}") 
makedepends=('kernel26-headers')
conflicts=('nvidia-96xx' 'nvidia-71xx' 'nvidia-legacy' 'nvidia') 
license=('custom') 
install=nvidia.install
source=("ftp://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/${_srcname}.run")

md5sums=('f6af8917a500de28396a438f3f548c88')
[ "$CARCH" = "x86_64" ] && md5sums=('7b5924f14a4d2326b4d916efdb7852ff')


build()
{

  # Extract the nvidia drivers
  cd $srcdir
  if [ -d ${_srcname} ]; then
    rm -rf ${_srcname}
  fi
  sh ${_srcname}.run --extract-only || return 1
  cd ${_srcname}/kernel
  make SYSSRC=/usr/src/linux-${_kernver} module  || return 1
}

package() {

  cd $srcdir/${_srcname}/kernel  

  # Install kernel module
  mkdir -p $startdir/pkg/lib/modules/${_kernver}/kernel/drivers/video/
  install -m644 nvidia.ko $startdir/pkg/lib/modules/${_kernver}/kernel/drivers/video/ || return 1

}