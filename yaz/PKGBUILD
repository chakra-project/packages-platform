#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=yaz
pkgver=4.0.3
pkgrel=1
pkgdesc="A toolkit supporting the development of Z39.50/SRW/SRU clients and servers"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.indexdata.dk/yaz"
depends=('openssl' 'libxslt' 'icu')
options=('!libtool')
source=(http://ftp.indexdata.dk/pub/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('3842adef3e0c919bc8de8888be7ce41a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --enable-shared=yaz \
    --with-openssl=/usr || return 1 
  make || return 1 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install || return 1 
  install -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1 
}
