# maintainer: Steve Holmes <steve.holmes88@gmail.com>
# Contributor: Alexander Jenisch <nt@divzero.at>
# Contributor: Chris Brannon <cmbrannon@cox.net>
# Contributor: Andreas Messer <andi@bupfen.de>

pkgname=speech-dispatcher
pkgver=0.6.7
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="High-level device independent layer for speech synthesis interface"
license=('GPL2' 'FDL')
depends=('glib2' 'alsa-lib')
optdepends=('festival-freebsoft-utils: Speech output using Festival.' 'espeak: Speech output using ESpeak.')
makedepends=('pkgconfig' 'espeak' 'dotconf' 'automake')
provides=("speechd=$pkgver")
options=('!libtool') 
url="http://www.freebsoft.org/speechd"
source=("http://www.freebsoft.org/pub/projects/speechd/$pkgname-$pkgver.tar.gz" \
  "spd-respect-DESTDIR.diff" \
  "speechd")

install="speech-dispatcher.install"

backup=(etc/speech-dispatcher/clients/gnome-speech.conf
	etc/speech-dispatcher/clients/emacs.conf
	etc/speech-dispatcher/modules/ibmtts.conf
	etc/speech-dispatcher/modules/espeak-generic.conf
	etc/speech-dispatcher/modules/espeak.conf
	etc/speech-dispatcher/modules/swift-generic.conf
	etc/speech-dispatcher/modules/festival.conf
	etc/speech-dispatcher/modules/cicero.conf
	etc/speech-dispatcher/modules/espeak-mbrola-generic.conf
	etc/speech-dispatcher/modules/dtk-generic.conf
	etc/speech-dispatcher/modules/llia_phon-generic.conf
	etc/speech-dispatcher/modules/ivona.conf
	etc/speech-dispatcher/modules/epos-generic.conf
	etc/speech-dispatcher/modules/flite.conf
	etc/speech-dispatcher/speechd.conf)

build() {
  cd $srcdir/$pkgname-$pkgver
  
  # One part of the build process ignores DESTDIR, so apply a tiny patch.
  # Patch has been submitted to upstream.
  patch -p1 < ${startdir}/spd-respect-DESTDIR.diff

  # Building speechd
  
  ./configure --prefix=/usr --sysconfdir=/etc || return 1
  make || return 1
  make DESTDIR=${pkgdir} moduleconfdir=/etc/speech-dispatcher/modules spdconfdir=/etc/speech-dispatcher install || return 1
  rm -f ${pkgdir}/usr/share/info/dir
  gzip -9nf ${pkgdir}/usr/share/info/*
  install -Dm755 ${srcdir}/speechd ${pkgdir}/etc/rc.d/speechd
  mkdir -p ${pkgdir}/var/log/speech-dispatcher
}
md5sums=('67432ad655b50fd7c1f1f79e012cfe3f'
         '4585be4a40ab33f375c31f6ab3a1915f'
         '53aece3bfea37d40dad62f02f7c5755e')
