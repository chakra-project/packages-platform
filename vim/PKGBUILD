# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Maintainer: tobias [ tobias at archlinux org ]
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=(vim)
pkgver=7.2
__hgrev=e12b9d992389
pkgrel=1
arch=('i686' 'x86_64')
license=('custom:vim')
url="http://www.vim.org"
makedepends=('gpm' 'perl' 'python' 'ruby' 'libxt' 'desktop-file-utils'
             'gettext' 'pkgconfig' 'mercurial' 'rsync' 'sed')
options=(force)
source=(pythoncomplete.vim::http://www.vim.org/scripts/download_script.php\?src_id=10872
        vimrc archlinux.vim)
md5sums=('6e7adfbd5d26c1d161030ec203a7f243' 'e57777374891063b9ca48a1fe392ac05'
         '10353a61aadc3f276692d0e17db1478e')

__hgroot='http://vim.googlecode.com/hg/'
__hgrepo='vim'
__hgbranch='default'

_versiondir="vim${pkgver//./}"

##### Build #####

build() {
  cd ${srcdir}

  msg2 'Checking out source from Mercurial...'

  if [[ -d ${__hgrepo} ]]; then
    cd ${__hgrepo}
    hg pull -b ${__hgbranch}|| warning 'hg pull failed!'
    hg update -r ${__hgrev} || return 1
  else
    hg clone -b ${__hgbranch} -u ${__hgrev} "${__hgroot}${__hgrepo}" ${__hgrepo} || return 1
    cd ${__hgrepo}
  fi

  if (( $(hg id -n) < $(hg id -nr ${__hgbranch}) )); then
    warning 'You are not building the latest revision!'
    warning "Consider updating __hgrev to $(hg id -r ${__hgbranch})."
    sleep 10
  fi

  cd ..
  rm -rf vim-build gvim-build
  rsync -a --exclude='.hg/' ${__hgrepo}/ vim-build || return 1

  msg2 'Patching...'

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h || return 1
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h || return 1

  msg2 'Building...'

  cp -a vim-build gvim-build || return 1

  cd ${srcdir}/vim-build

  ./configure --prefix=/usr --localstatedir=/var/lib/vim \
    --mandir=/usr/share/man --with-compiledby=Chakra \
    --with-features=big --enable-gpm --enable-acl --with-x=no \
    --disable-gui --enable-multibyte --enable-cscope \
    --disable-netbeans --enable-perlinterp --disable-pythoninterp \
    --disable-rubyinterp || return 1

  make || return 1
}

##### Packaging #####

package() {
  pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
  depends=('gpm' 'perl')
  backup=(etc/vimrc)

  cd ${srcdir}/vim-build
  make -j1 VIMRCLOC=/etc DESTDIR=${pkgdir} install || return 1

  # provided by (n)vi in core
  rm ${pkgdir}/usr/bin/{ex,view}

  # delete some manpages
  find ${pkgdir}/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # Don't forget logtalk.dict
  install -Dm644 ${srcdir}/gvim-build/runtime/ftplugin/logtalk.dict \
    ${pkgdir}/usr/share/vim/${_versiondir}/ftplugin/logtalk.dict || return 1

  # make Aaron happy
  install -Dm644 ${srcdir}/pythoncomplete.vim \
    ${pkgdir}/usr/share/vim/vim72/autoload/pythoncomplete.vim
  
  # rc files
  install -Dm644 ${srcdir}/vimrc ${pkgdir}/etc/vimrc
  install -Dm644 ${srcdir}/archlinux.vim \
    ${pkgdir}/usr/share/vim/vimfiles/archlinux.vim

  # license
  install -dm755 ${pkgdir}/usr/share/licenses/vim
  ln -s /usr/share/vim/${_versiondir}/doc/uganda.txt \
    ${pkgdir}/usr/share/licenses/vim/license.txt
}


# vim:set sw=2 sts=2 et:
